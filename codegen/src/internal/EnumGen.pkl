//===----------------------------------------------------------------------===//
// Copyright Â© 2024-2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//   https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//

@Unlisted
module pkl.swift.internal.EnumGen

extends "Gen.pkl"

import "pkl:reflect"
import "SwiftMapping.pkl"
import "utils.pkl"
import "typegen.pkl"
import "Type.pkl"

alias: reflect.TypeAlias = mapping.source as reflect.TypeAlias

enumMapping: SwiftMapping.Enum = mapping as SwiftMapping.Enum

local unionType: reflect.UnionType = alias.referent as reflect.UnionType

local unionMembers = unionType.members.filter((it) -> !(it is reflect.NothingType))

local isStringLiteralUnion =
  let (stringLiterals = unionMembers.filterIsInstance(reflect.StringLiteralType))
    unionMembers.length == stringLiterals.length

local enumBaseMembers: List<EnumMember> = unionMembers
  .map((it) ->
    new EnumMember {
      pklType = it
      renderedType = type.render(module.namespaceName)
      type = typegen.generateType(it, module.mapping.source, module.mappings)
    }
  )

local enumStringLiteralMembers = enumBaseMembers.map((it) -> (it) {
  name = utils.normalizeEnumName((it.pklType as reflect.StringLiteralType).value)
})

local enumNormalMembers = enumBaseMembers.map((it) -> (it) {
  name =
    let (baseName = utils.normalizeEnumName(renderedType.replaceFirst(Regex("^any "), "")))
      if (type is Type.Dictionary) "dictionary" + baseName.capitalize()
      else if (type is Type.Array) "array" + baseName.capitalize()
      else baseName
})

local enumNonNumericMembers = enumNormalMembers.filter((it) -> !it.type.isNumeric)

local enumNumericMembers = enumNormalMembers.filter((it) -> it.type.isNumeric)

contents =
  if (isStringLiteralUnion) stringLiteralEnumContents
  else enumContents

local stringLiteralEnumContents =
  new Listing {
    when (alias.docComment != null) {
      utils.renderDocComment(alias.docComment!!, "")
    }
    "public enum \(module.mapping.name): String, CaseIterable, CodingKeyRepresentable, Decodable, Hashable, Sendable {"
    for (member in enumStringLiteralMembers) {
      "\(module.indent)case \(member.name) = \(utils.toSwiftString((member.pklType as reflect.StringLiteralType).value))"
    }
    "}"
  }.join("\n")

local enumContents =
  new Listing {
    when (alias.docComment != null) {
      utils.renderDocComment(alias.docComment!!, "")
    }
    "public enum \(module.mapping.name): Decodable, Hashable, Sendable {"
    for (member: EnumMember in enumNormalMembers) {
      "\(module.indent)case \(member.name)(\(member.renderedType))"
    }
    ""
    when (!enumNumericMembers.isEmpty) {
      decodeNumeric
      ""
    }
    when (enumNormalMembers.any((it) -> it.type.isPolymorphic)) {
      synthesizedEquals
      ""
    }
    initMethod
    when (enumNormalMembers.any((it) -> it.type.isPolymorphic)) {
      ""
      synthesizedHash
    }
    "}"
  }.join("\n")

local throwTypeMismatch = new Listing {
  "\(module.indent.repeat(2))throw DecodingError.typeMismatch("
  "\(module.indent.repeat(3))\(module.mapping.name).self,"
  "\(module.indent.repeat(3)).init("
  "\(module.indent.repeat(4))codingPath: decoder.codingPath,"
  "\(module.indent.repeat(4))debugDescription: \"Expected type \(module.mapping.name), but got \\(String(describing: value))\""
  "\(module.indent.repeat(3)))"
  "\(module.indent.repeat(2)))"
}.join("\n")

local initMethod = new Listing {
  "\(module.indent)public init(from decoder: Decoder) throws {"
  "\(module.indent.repeat(2))let container = try decoder.singleValueContainer()"
  when (!enumNumericMembers.isEmpty) {
    "\(module.indent.repeat(2))let decoded = \(module.mapping.name).decodeNumeric(from: decoder, container)"
    "\(module.indent.repeat(2))if decoded != nil {"
    "\(module.indent.repeat(3))self = decoded!"
    "\(module.indent.repeat(3))return"
    "\(module.indent.repeat(2))}"
  }
  "\(module.indent.repeat(2))let value = try container.decode(PklSwift.PklAny.self).value"
  when (!enumNonNumericMembers.isEmpty) {
    "\(module.indent.repeat(2))switch value?.base {"
    for (member in enumNonNumericMembers) {
      "\(module.indent.repeat(2))case let decoded as \(member.renderedType):"
      "\(module.indent.repeat(3))self = \(module.mapping.name).\(member.name)(decoded)"
    }
    "\(module.indent.repeat(2))default:"
    // indent one more level
    module.indent + throwTypeMismatch.split("\n").join("\n" + module.indent)
    "\(module.indent.repeat(2))}"
  } else {
    throwTypeMismatch
  }
  "\(module.indent)}"
}
  .join("\n")

local synthesizedEquals = new Listing {
  "\(module.indent)public static func ==(lhs: \(module.mapping.name), rhs: \(module.mapping.name)) -> Bool {"
  "\(module.indent.repeat(2))switch (lhs, rhs) {"
  for (member in enumNormalMembers) {
    "\(module.indent.repeat(2))case let (.\(member.name)(a), .\(member.name)(b)):"
    when (member.type.isPolymorphic) {
      "\(module.indent.repeat(3))return a.isDynamicallyEqual(to: b)"
    } else {
      "\(module.indent.repeat(3))return a == b"
    }
  }
  "\(module.indent.repeat(2))default:"
  "\(module.indent.repeat(3))return false"
  "\(module.indent.repeat(2))}"
  "\(module.indent)}"
}.join("\n")

local synthesizedHash = new Listing {
  "\(module.indent)public func hash(into hasher: inout Hasher) {"
  "\(module.indent.repeat(2))switch self {"
  for (member in enumNormalMembers) {
    "\(module.indent.repeat(2))case let .\(member.name)(value):"
    "\(module.indent.repeat(3))hasher.combine(value)"
  }
  "\(module.indent.repeat(2))}"
  "\(module.indent)}"
}.join("\n")

local decodeNumeric = new Listing {
  "\(module.indent)private static func decodeNumeric(from decoder: Decoder, _ container: any SingleValueDecodingContainer) -> \(module.mapping.name)? {"
  when (enumNumericMembers.length == 1) {
    local member = enumNumericMembers.first
    "\(module.indent.repeat(2))return try? .\(member.name)(container.decode(\(member.renderedType).self))"
  } else {
    for (idx, member in enumNumericMembers) {
      when (idx == 0) {
        "\(module.indent.repeat(2))return (try? .\(member.name)(container.decode(\(member.renderedType).self)))"
      } else {
        "\(module.indent.repeat(3))?? (try? .\(member.name)(container.decode(\(member.renderedType).self)))"
      }
    }
  }
  "\(module.indent)}"
}.join("\n")

local class EnumMember {
  name: String
  type: Type
  renderedType: String
  pklType: reflect.Type
}
